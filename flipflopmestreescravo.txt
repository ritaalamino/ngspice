*Somador Carry-Lookahead para 4 bits

*PARAMETROS
    .include 32nm_HP.pm 

* Declarando Fontes de tensão
    Vvdd vdd gnd 0.9

* Declaração das fontes
    Va0 a0 gnd PWL (0n 0 10n 0 10.01n 0.9 20n 0.9)
    Vb0 b0 gnd PWL (0n 0 5n 0 5.01n 0.9 10n 0.9 10.01n 0 15n 0 15.01n 0.9 20n 0.9)
    Vc0 c0 gnd 0

    Va1 a1 gnd PWL (0n 0 10n 0 10.01n 0.9 20n 0.9)
    Vb1 b1 gnd PWL (0n 0 5n 0 5.01n 0.9 10n 0.9 10.01n 0 15n 0 15.01n 0.9 20n 0.9)

    Va2 a2 gnd PWL (0n 0 10n 0 10.01n 0.9 20n 0.9)
    Vb2 b2 gnd PWL (0n 0 5n 0 5.01n 0.9 10n 0.9 10.01n 0 15n 0 15.01n 0.9 20n 0.9)

    Va3 a3 gnd PWL (0n 0 10n 0 10.01n 0.9 20n 0.9)
    Vb3 b3 gnd PWL (0n 0 5n 0 5.01n 0.9 10n 0.9 10.01n 0 15n 0 15.01n 0.9 20n 0.9)

*Instanciação
    * Generate 0 e Propagate 0
        Xg0 a0 b0 vdd gnd g0 AND
        Xp0 a0 b0 vdd gnd p0 XOR

    * Generate 1 e Propagate 1
        Xg1 a1 b1 vdd gnd g1 AND
        Xp1 a1 b1 vdd gnd p1 XOR

    * Generate 2 e Propagate 2
        Xg2 a2 b2 vdd gnd g2 AND
        Xp2 a2 b2 vdd gnd p2 XOR

    * Generate 3 e Propagate 3
        Xg3 a3 b3 vdd gnd g3 AND
        Xp3 a3 b3 vdd gnd p3 XOR

    * Soma 0
        Xsum0 c0 p0 vdd gnd sum0 XOR

    * Soma 1 e C_out 1
        Xn1 c0 p0 vdd gnd n1 AND
        
        Xc_out1 g0 n1 vdd gnd c_out1 OR
        Xsum1 c_out1 p1 vdd gnd sum1 XOR

    * Soma 2 e C_out 2
        Xn2 n1 p1 vdd gnd n2 AND
        Xn3 g0 p1 vdd gnd n3 AND
        Xn4 n2 n3 vdd gnd n4 OR
        
        Xc_out2 n4 g1 vdd gnd c_out2 OR 
        Xsum2 c_out2 p2 vdd gnd sum2 XOR

    * Soma 3 e C_out 3
        Xn5 n2 p2 vdd gnd n5 AND
        Xn6 n3 p2 vdd gnd n6 AND
        Xn7 g1 p2 vdd gnd n7 AND
        Xn8 n5 n6 vdd gnd n8 OR
        Xn9 n7 g2 vdd gnd n9 OR
        
        Xc_out3 n8 n9 vdd gnd c_out3 OR   
        Xsum3 c_out3 p3 vdd gnd sum3 XOR

    * C_out 4
        Xn10 n5 p3 vdd gnd n10 AND
        Xn11 n6 p3 vdd gnd n11 AND
        Xn12 n7 p3 vdd gnd n12 AND
        Xn13 g2 p3 vdd gnd n13 AND
        Xn14 n10 n11 vdd gnd n14 OR
        Xn15 n12 n13 vdd gnd n15 OR
        Xn16 n14 n15 vdd gnd n16 OR
        
        Xc_out4 n16 g3 vdd gnd c_out4 OR

* Capacitância da saída
    Csum0 sum0 gnd 1f
    Csum1 sum1 gnd 1f
    Csum2 sum2 gnd 1f
    Csum3 sum3 gnd 1f

    Ccout1 c_out1 gnd 1f
    Ccout2 c_out2 gnd 1f
    Ccout3 c_out3 gnd 1f
    Ccout4 c_out4 gnd 1f

*Subcircuitos de portas lógicas

*Xor - CMOS

*Inversor1 - Cmos

.Subckt INV a vdd gnd s

    *Rede Pull up
        Mp1 s a vdd vdd PMOS W=128n L=32n

    *Rede Pull down
        Mn1 s a gnd gnd NMOS W=64n L=32n    

.Ends INV

.Subckt XOR a b vdd gnd s

    *Inversores
        X1a a vdd gnd a_not INV
        X1b b vdd gnd b_not INV

    *Rede Pull up
        Mpa_n n1 a_not vdd vdd PMOS W=128n L=32n
        Mpb s b n1 vdd PMOS W=128n L=32n

        Mpa n2 a vdd vdd PMOS W=128n L=32n
        Mpb_n s b_not n2 vdd PMOS W=128n L=32n 

    *Rede Pull down
        Mnb n3 b gnd gnd NMOS W=64n L=32n
        Mna s a n3 gnd NMOS W=64n L=32n

        Mnb_n n4 b_not gnd gnd NMOS W=64n L=32n
        Mna_n s a_not n4 gnd NMOS W=64n L=32n

.Ends XOR

*Or - CMOS

.Subckt OR a b vdd gnd s

    *Rede Pull up
        Mpa n a vdd vdd PMOS W=128n L=32n
        Mpb x b n vdd PMOS W=128n L=32n

        *Inversor
        Mps s x vdd vdd PMOS W=128n L=32n

    *Rede Pull down
        Mna x b gnd gnd NMOS W=64n L=32n
        Mnb x a gnd gnd NMOS W=64n L=32n

        *Inversor
        Mns s x gnd gnd NMOS W=64n L=32n

.Ends OR

*And - CMOS

.Subckt AND a b vdd gnd s

    *Rede Pull up
        Mpa x a vdd vdd PMOS W=128n L=32n
        Mpb x b vdd vdd PMOS W=128n L=32n

        *Inversor
        Mps s x vdd vdd PMOS W=128n L=32n

    *Rede Pull down
        Mna n b gnd gnd NMOS W=64n L=32n
        Mnb x a n gnd NMOS W=64n L=32n

        *Inversor
        Mns s x gnd gnd NMOS W=64n L=32n

.Ends AND

* Simulação Transiente de 20ns com passo de 0.1ns
    .tran 0.1ns 20ns

.control
    run
    *Variaveis que existem para poder serem plotadas
    *plot a0 b0 c0 sum0 c_out1 a1 b1 c_out1 sum1 c_out2 a2 b2 c_out2 sum2 c_out3 a3 b3 c_out3 sum3 c_out4

.endc

* Fim do Arquivo SPICE
    .end        


