*------ Simulando um LATCH-SR com enable

*---Include da tecnologia---
.include 32nm_HP.pm

*--- Declarando um sub-circuito ---
.SUBCKT INV in out supply ground
M1 out in supply supply pmos l=32n w=150n
M2 out in ground ground nmos l=32n w=100n
.ends INV

.SUBCKT AND in1 in2 out aliment terra
MP1 nand  in1 aliment aliment pmos l=32n w=150n
MP2 nand  in2 aliment aliment pmos l=32n w=150n
MN1 nand  in1 nodo1   terra   nmos l=32n w=200n
MN2 nodo1 in2 terra   terra   nmos l=32n w=200n
Xinv nand out aliment terra INV
.ends AND

.SUBCKT NOR in1 in2 out aliment terra
MP1 nodo1 in1 aliment aliment pmos l=32n w=300n
MP2 out   in2 nodo1   aliment pmos l=32n w=300n
MN1 out   in1 terra   terra   nmos l=32n w=100n
MN2 out   in2 terra   terra   nmos l=32n w=100n
.ends NOR



*----Declaracao das fontes---
* Fonte de Alimentação
Vvdd vdd gnd 0.9V
* Fonte descrevendo o sinal de entrada
Vs s gnd PULSE (0.9 0 1n 5p 5p 3n 6n)
Vr r gnd PULSE (0 0.9 2n 5p 5p 1n 3n)
Ven en gnd PULSE (0.9 0 10.5n 5p 5p 16n 28n)
*---- Declaração do circuito

Xand1 r  en rr vdd gnd AND
Xand2 s  en ss vdd gnd AND
Xnor1 ss q  qb  vdd gnd NOR
Xnor2 rr qb q vdd gnd NOR

* Capacitância/Circuito de Saída
*Cload1 q gnd 1f
*Cload2 qb gnd 1f

*----Definicao do tipo de simulacao
.tran 1p 50n

.end